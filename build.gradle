buildscript {
    repositories {
        maven {
            url 'http://maven.milanaleksic.net/release'
        }
    }
    dependencies {
        classpath 'net.milanaleksic.gradle:multiplatform:0.8'
    }
}

import static net.milanaleksic.gradle.multiplatform.MultiPlatformAppPlugin.ARCH_X86
import static net.milanaleksic.gradle.multiplatform.MultiPlatformAppPlugin.ARCH_X86_64
import static net.milanaleksic.gradle.multiplatform.MultiPlatformAppPlugin.ARCH_ARM
import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_UNIX
import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS

apply plugin: 'java'
apply plugin: 'idea'

ext.swtVersion = "4.2"
ext.jacksonVersion = "2.3.2"
ext.springVersion = "3.1.1.RELEASE"

allprojects {

    apply plugin: 'multiplatform'

    group = 'net.milanaleksic.baobab'
    version = baobabVersion
    description = 'Baobab toolkit for building interfaces in SWT using JSON'

    sourceCompatibility = sourceVersion
    targetCompatibility = sourceVersion

    repositories {
        mavenCentral()
        maven {
            url 'http://maven.milanaleksic.net/external'
        }
        maven {
            url 'http://maven.milanaleksic.net/release'
        }
    }

    configurations {
        win32
        win64
        linux32
        linux64
        linuxArm
        compile
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    multiplatform {
        version = project.version

        dependencyMappings {
            dependencyMapping('win32', FAMILY_WINDOWS, ARCH_X86)
            dependencyMapping('win64', FAMILY_WINDOWS, ARCH_X86_64)
            dependencyMapping('linux32', FAMILY_UNIX, ARCH_X86)
            dependencyMapping('linux64', FAMILY_UNIX, ARCH_X86_64)
            dependencyMapping('linuxArm', FAMILY_UNIX, ARCH_ARM)
        }
    }

    dependencies {
        win32 "org.eclipse.swt:org.eclipse.swt.win32.win32.x86:$swtVersion"
        win64 "org.eclipse.swt:org.eclipse.swt.win32.win32.x86_64:$swtVersion"
        linux32 "org.eclipse.swt:org.eclipse.swt.gtk.linux.x86:$swtVersion"
        linux64 "org.eclipse.swt:org.eclipse.swt.gtk.linux.x86_64:$swtVersion"
        linuxArm "org.eclipse.swt:org.eclipse.swt.gtk.linux.arm:3.8.2"

        testCompile "junit:junit-dep:4.10"
        testCompile "org.hamcrest:hamcrest-core:1.2.1"
        testCompile "org.hamcrest:hamcrest-library:1.2.1"
    }

    apply from: "$rootDir/deployment.gradle"
}
